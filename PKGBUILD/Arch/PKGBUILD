# Maintainer: Nicholas Girga <contact@nickgirga.com>
pkgname=chocolate-box-git
pkgver=r44.df6e037
pkgrel=1
pkgdesc="Simple GUI for Chocolate DOOM"
arch=('any')
url="https://gitlab.com/nickgirga/chocolate-box"
license=('MIT')
depends=('python'
 	 'gtk3'
 	 'python-gobject')
makedepends=('git')
optdepends=('chocolate-doom'
	    'crispy-doom'
	    'zdoom'
	    'gzdoom'
	    'freedm'
	    'freedoom1'
	    'freedoom2'
	    'doom1-wad'
	    'heretic1-wad'
	    'hexen1-wad')
source=("$pkgname::git+https://gitlab.com/nickgirga/chocolate-box.git")
md5sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	cd "$srcdir/$pkgname"
	install -Dm755 ./chocolate-box "$pkgdir/usr/bin/chocolate-box"
	install -Dm644 ./chocolate-box.ui "$pkgdir/usr/share/chocolate-box/chocolate-box.ui"
	install -Dm644 ./res/icon/icon_128.png "$pkgdir/usr/share/icons/chocolate-box_128.png"
	install -Dm644 ./res/icon/icon_256.png "$pkgdir/usr/share/icons/chocolate-box_256.png"
	install -Dm644 ./chocolate-box.desktop "$pkgdir/usr/share/applications/chocolate-box.desktop"
}
